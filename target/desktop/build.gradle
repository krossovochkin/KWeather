import org.jetbrains.compose.desktop.application.dsl.TargetFormat

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            url "https://maven.pkg.jetbrains.space/public/p/compose/dev"
        }
    }
    dependencies {
        classpath libs.gradle.plugin.kotlin
        classpath libs.gradle.plugin.compose
    }
}

plugins {
    id("kotlin-multiplatform")
    alias(libs.plugins.jetbrains.compose)
    alias(libs.plugins.compose.compiler)
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.pkg.jetbrains.space/public/p/compose/dev"
    }
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = "11"
        }
    }
    sourceSets {
        jvmMain {
            dependencies {
                implementation compose.desktop.currentOs
                implementation project(":core:image-loader:jvm")
                implementation project(":shared")
                implementation libs.kodein.di
                implementation libs.ktor.client.java
            }
        }
    }
}

compose.desktop {
    application {
        mainClass = "com.krossovochkin.kweather.MainKt"
        nativeDistributions {
            targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
            packageName = "jvm"
        }
        args(getApiKey())
    }
}

String getApiKey() {
    def ciKey = System.getenv()['API_KEY']
    def localKey = findProperty("kweather_api_key")
    if (ciKey != null) {
        return ciKey
    } else if (localKey != null) {
        return localKey
    } else {
        throw Exception("Can't find API key")
    }
}
